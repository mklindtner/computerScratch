// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //decode w/o jmp
    DecodeChip(i=instruction[15], d1=instruction[5], d2=instruction[4], d3=instruction[3], 
    loadA=d1, loadD=d2, writeM=writeM);

    Mux16(a[0..14]=instruction[0..14], b=aluOut, sel=instruction[15], out=m1);
    ARegister(in=m1, load=d1, out=aRegOut, out[0..14]=addressM);
    
    Mux16(a=aRegOut, b=inM, sel=instruction[12], out=m2);
    DRegister(in=aluOut, load=d2, out=dRegOut);

    ALU(x=dRegOut, y=m2, zx=instruction[11], nx=instruction[10], zy=instruction[9], 
    ny=instruction[8], f=instruction[7], no=instruction[6], zr=zrOut, ng=ngOut, out=aluOut, 
    out=outM);

    JumpCond(zr=zrOut, ng=ngOut, J1=instruction[2], J2=instruction[1], J3=instruction[0], 
    out=cLoadPC);

    And(a=cLoadPC, b=instruction[15], out=loadPC);
    PC(in=aRegOut, reset=reset, load=loadPC, inc=true, out[0..14]=pc);

}
